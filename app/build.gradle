apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'realm-android'

//noinspection GroovyAssignabilityCheck
if (project.hasProperty("LibertVendas.signing")
        && new File(project.property("LibertVendas.signing") + ".gradle").exists()) {
    apply from: project.property("LibertVendas.signing") + ".gradle";
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "br.com.libertsolutions.libertvendas.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 25100
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "LibertVendas-$versionName")
        manifestPlaceholders = [applicationLabel: "@string/app_name"]
        resConfigs "pt-rBR"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            manifestPlaceholders = [applicationLabel: "Libert Dev"]
            buildConfigField "long", "SCHEMA_VERSION", "1L"
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFile 'proguard-butterknife.pro'
            proguardFile 'proguard-circular-progress-button.pro'
            proguardFile 'proguard-crashlytics.pro'
            proguardFile 'proguard-eventbus-3.pro'
            proguardFile 'proguard-gson.pro'
            proguardFile 'proguard-joda-time.pro'
            proguardFile 'proguard-joda-time-android.pro'
            proguardFile 'proguard-material-sheet-fab.pro'
            proguardFile 'proguard-mpandroidchart.pro'
            proguardFile 'proguard-okhttp3.pro'
            proguardFile 'proguard-retrofit2.pro'
            proguardFile 'proguard-retrolambda.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-square-okio.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
            proguardFile 'proguard-support-v7-cardview.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "long", "SCHEMA_VERSION", "1L"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        jumboMode true
    }
}

ext {
    support = '25.3.1'
    butterknife = '8.5.1'
    retrofit = '2.2.0'
    okhttp = '3.6.0'
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    // Support library
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile ("com.takisoft.fix:preference-v7:${support}.0", {
        exclude group: 'android.support.annotation', module: 'support-annotations'
    })

    // Play Services
    compile 'com.google.android.gms:play-services-gcm:10.2.4'

    // View Injection
    compile "com.jakewharton:butterknife:${butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife}"

    //Reactive Extensions
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.6'

    // Http
    compile ("com.squareup.retrofit2:retrofit:${retrofit}", {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    })
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.okhttp3:okhttp:${okhttp}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp}"

    // Progress indicator
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    //Event Bus
    compile 'org.greenrobot:eventbus:3.0.0'

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Material design
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    compile('com.mikepenz:materialdrawer:5.9.0@aar') {
        transitive = true
        exclude module: "fastadapter"
    }
    compile 'com.afollestad.material-dialogs:core:0.9.4.3'
    compile 'com.github.arimorty:floatingsearchview:2.0.3'
    compile 'com.stepstone.stepper:material-stepper:3.2.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.code-troopers.betterpickers:library:3.1.0'
    compile 'com.borax12.materialdaterangepicker:library:1.9'

    // Chart
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    // Phone number parsing, formatting, and validating
    compile 'com.googlecode.libphonenumber:libphonenumber:8.3.3'

    // Java date and time
    compile 'net.danlew:android.joda:2.9.9'

    // RecyclerView adapter
    compile('com.mikepenz:fastadapter:2.5.1@aar') { transitive = true }
    compile 'com.mikepenz:fastadapter-commons:2.5.0@aar'
    compile 'com.mikepenz:fastadapter-extensions:2.5.0@aar'

    // Crash reporting
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
}

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

dexcount {
    format = "json"
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}