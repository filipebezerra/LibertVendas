buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

//noinspection GroovyAssignabilityCheck
if (project.hasProperty("LibertVendas.signing")
        && new File(project.property("LibertVendas.signing") + ".gradle").exists()) {
    apply from: project.property("LibertVendas.signing") + ".gradle";
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "br.com.libertsolutions.libertvendas.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 25092
        versionName "0.9.2-beta"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "LibertVendas-$versionName")
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            ext.enableCrashlytics = false
        }
        release {
            ext.enableCrashlytics = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        mock {

        }
        prod {

        }
    }
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support = '25.1.0'
    butterknife = '8.5.1'
    retrofit = '2.1.0'
    okhttp = '3.5.0'
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/jlmd/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    // Android Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // JVM Testing
    testCompile 'junit:junit:4.12'

    // Support library
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile ("com.takisoft.fix:preference-v7:${support}.2", {
        exclude group: 'android.support.annotation', module: 'support-annotations'
    })

    // Play Services
    compile 'com.google.android.gms:play-services-gcm:10.0.1'

    // View Injection
    compile "com.jakewharton:butterknife:${butterknife}"
    apt "com.jakewharton:butterknife-compiler:${butterknife}"

    //Reactive Extensions library
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.4'

    //Event Bus library
    compile 'org.greenrobot:eventbus:3.0.0'

    // Material Design libraries
    compile('com.mikepenz:materialdrawer:5.8.1@aar') { transitive = true }
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'
    compile 'com.github.smtchahal:MaterialSpinner:1.1.7'
    compile 'com.code-troopers.betterpickers:library:3.1.0'

    // Progress Indicator/Loading library
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    compile 'com.github.jlmd:AnimatedCircleLoadingView:1.1.5@aar'

    // Logging library
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Network libraries
    compile ("com.squareup.retrofit2:retrofit:${retrofit}", {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    })
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.okhttp3:okhttp:${okhttp}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp}"

    // Phone number formatting library
    compile 'com.googlecode.libphonenumber:libphonenumber:8.1.0'

    // Crash reporting
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // Chart
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
}

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

dexcount {
    format = "json"
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}