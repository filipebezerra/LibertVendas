apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

//noinspection GroovyAssignabilityCheck
if (project.hasProperty("LibertVendas.signing")
        && new File(project.property("LibertVendas.signing") + ".gradle").exists()) {
    apply from: project.property("LibertVendas.signing") + ".gradle";
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "br.com.libertsolutions.libertvendas.app"
        minSdkVersion 16
        targetSdkVersion 25
        // 25 + 060 + 1
        // 25   = Major Android version
        // 060  = Production version (Semantic versioning)
        // 1    = Pre-release version (Internal versioning)
        versionCode 25062
        versionName "0.6.2-beta"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "LibertVendas-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        mock {

        }
        prod {

        }
    }
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/jlmd/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

ext {
    support = '25.0.1'
    butterknife = '8.4.0'
    retrofit = '2.1.0'
    okhttp = '3.5.0'
}

dependencies {
    testCompile 'junit:junit:4.12'

    // Support library
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile ("com.takisoft.fix:preference-v7:${support}.0", {
            exclude group: 'android.support.annotation', module: 'support-annotations'
    })

    // View injection library
    compile "com.jakewharton:butterknife:${butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife}"

    // Logging library
    compile 'com.jakewharton.timber:timber:4.3.1'

    //Event Bus library
    compile 'org.greenrobot:eventbus:3.0.0'

    //Reactive Extensions library
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.3'

    // Material components libraries
    compile 'com.github.smtchahal:MaterialSpinner:1.1.7'
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.0.2'
    compile 'com.code-troopers.betterpickers:library:3.0.1'

    // Phone number formatting library
    compile 'com.googlecode.libphonenumber:libphonenumber:7.7.2'

    // Progress Indicator/Loading library
    compile 'com.github.jlmd:AnimatedCircleLoadingView:1.1.5@aar'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    // Http libraries
    compile ("com.squareup.retrofit2:retrofit:${retrofit}", {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    })
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.okhttp3:okhttp:${okhttp}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp}"

    // Crash reporting library
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
}

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

dexcount {
    format = "json"
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}