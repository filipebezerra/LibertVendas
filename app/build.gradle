apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "br.com.libertsolutions.libertvendas.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 25080
        versionName "0.8.0-beta"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "LibertVendas-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        mock {

        }
        prod {

        }
    }
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support = '25.1.0'
    butterknife = '8.4.0'
    retrofit = '2.1.0'
    okhttp = '3.5.0'
}

repositories {
    maven { url "http://dl.bintray.com/jlmd/maven" }
}

dependencies {
    // Android Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // JVM Testing
    testCompile 'junit:junit:4.12'

    // Support library
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile ("com.takisoft.fix:preference-v7:25.0.1.0", {
        exclude group: 'android.support.annotation', module: 'support-annotations'
    })

    // Material Design libraries
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'

    // Injection
    compile "com.jakewharton:butterknife:${butterknife}"
    apt "com.jakewharton:butterknife-compiler:${butterknife}"

    // Http libraries
    compile ("com.squareup.retrofit2:retrofit:${retrofit}", {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    })
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.okhttp3:okhttp:${okhttp}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp}"

    //Reactive Extensions library
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.4'

    //Event Bus library
    compile 'org.greenrobot:eventbus:3.0.0'

    // Logging library
    compile 'com.jakewharton.timber:timber:4.4.0'

    // Progress Indicator/Loading library
    compile 'com.github.jlmd:AnimatedCircleLoadingView:1.1.5@aar'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    // Phone number formatting library
    compile 'com.googlecode.libphonenumber:libphonenumber:8.0.0'
}

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

dexcount {
    format = "json"
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}